apply plugin: 'com.android.application'
apply plugin: 'com.google.gms.google-services'
apply plugin: 'com.google.firebase.crashlytics'

Properties properties = new Properties()
properties.load(project.rootProject.file('local.properties').newDataInputStream())

android {
    compileSdkVersion 36
    buildToolsVersion "35.0.0"

    defaultConfig {
        applicationId "com.xseth.homey"
        minSdkVersion 23
        targetSdkVersion 36
        versionCode 4
        versionName "1.1.2"

        // should correspond to key/value pairs inside the file
        buildConfigField("String", "ATHOM_CLIENT_ID", properties.getProperty('CLIENT_ID'))
        buildConfigField("String", "ATHOM_CLIENT_SECRET", properties.getProperty('CLIENT_SECRET'))
    }

    buildTypes {
        customDebugType {
            debuggable true
        }
        release {
            // Enables code shrinking, obfuscation, and optimization for only
            // your project's release build type.
            minifyEnabled true

            // Enables resource shrinking, which is performed by the
            // Android Gradle plugin.
            shrinkResources true

            // Includes the default ProGuard rules files that are packaged with
            // the Android Gradle plugin. To learn more, go to the section about
            // R8 configuration files.
            proguardFiles getDefaultProguardFile(
                    'proguard-android-optimize.txt'),
                    'proguard-rules.pro'
            debuggable false
            jniDebuggable false
            renderscriptDebuggable false
            renderscriptOptimLevel 3

        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }
    namespace 'com.xseth.homey'
}

dependencies {
    implementation fileTree(dir: "libs", include: ["*.jar"])
    implementation 'com.google.android.support:wearable:2.9.0'
    implementation 'androidx.recyclerview:recyclerview:1.3.2'

    // add the Firebase SDK for Google Analytics
    implementation 'com.google.firebase:firebase-analytics:22.1.2'
    // Add the Firebase Crashlytics SDK.
    implementation 'com.google.firebase:firebase-crashlytics:19.2.1'

    implementation 'androidx.wear:wear:1.3.0'
    implementation 'androidx.core:core:1.15.0'

    implementation 'androidx.legacy:legacy-support-core-utils:1.0.0'
    implementation 'androidx.legacy:legacy-support-core-ui:1.0.0'
    implementation 'androidx.appcompat:appcompat:1.7.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.2.0'
    implementation 'androidx.security:security-crypto:1.1.0-alpha06'
    compileOnly 'com.google.android.wearable:wearable:2.9.0'

    // Retrofit, HTTP client
    implementation 'com.squareup.retrofit2:retrofit:2.11.0'
    implementation 'com.squareup.retrofit2:converter-gson:2.11.0'
    implementation("com.squareup.okhttp3:logging-interceptor:4.12.0")

    // Room components
    implementation "androidx.room:room-runtime:$rootProject.roomVersion"
    annotationProcessor "androidx.room:room-compiler:$rootProject.roomVersion"
    androidTestImplementation "androidx.room:room-testing:$rootProject.roomVersion"

    // Lifecycle components
    implementation "androidx.lifecycle:lifecycle-extensions:$rootProject.archLifecycleVersion"
    annotationProcessor "androidx.lifecycle:lifecycle-compiler:$rootProject.archLifecycleVersion"

    // UI
    implementation "com.google.android.material:material:$rootProject.materialVersion"

    // Logging
    implementation 'com.jakewharton.timber:timber:5.0.1'

    // Testing
    androidTestImplementation "androidx.arch.core:core-testing:$rootProject.coreTestingVersion"
}